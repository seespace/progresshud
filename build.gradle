buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "com.android.tools.build:gradle:1.1.0"
  }
}

apply plugin: 'com.android.library'

android {
  compileSdkVersion 19
  buildToolsVersion "20.0.0"

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  defaultConfig {
    minSdkVersion 19
    targetSdkVersion 19
    versionCode 1
    versionName "1.0.1"
  }

  version '1.0.1'
}

repositories {
  jcenter()
  maven {
    url "https://raw.githubusercontent.com/seespace/maven/master/"
  }
}

def isProd() {
  return System.getenv("PRODUCTION") != null;
}

def library(projectName, boolean extra) {
  if (isProd()) {
    println "adding binary dependency on project $projectName"
    if (extra) {
      dependencies.create(group: "${extra_groupId}", name: "$projectName".substring(1), version: "+")
    } else {
      dependencies.create(group: "${framework_groupId}", name: "$projectName".substring(1), version: "+")
    }
  } else {
    println "adding project dependency on project $projectName"
    project("$projectName")
  }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  compile library(':inair', false)
  compile 'org.jdeferred:jdeferred-core:1.2.3'
  compile 'org.jdeferred:jdeferred-android-aar:1.2.3'
}

apply plugin: 'maven'

uploadArchives {
  repositories.mavenDeployer {
    repository(url: "file://${file("${repo_path}").absolutePath}")
    pom.project {
      groupId "${extra_groupId}"
      artifactId project.name
      version project.version
    }
  }
}
